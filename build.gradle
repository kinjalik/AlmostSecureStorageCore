plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'maven-publish'
}

group 'ru.kinjalik'
version '0.2.5'

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

dependencies {
    runtimeOnly "org.jetbrains.kotlin:kotlin-stdlib:1.5.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.0"

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    ktlint "com.pinterest:ktlint:0.41.0"

}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kinjalik/AlmostSecureStorageCore")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {

        gpr(MavenPublication) {
            groupId = "ru.kinjalik"
            artifactId = "almost-secure-storage-core"
            version = project.version
            from(components.java)
        }
    }
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

task lint(type: JavaExec, group: "verification") {
    description = "Kotlin code style check."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt --disabled-rules=import-ordering"
}
